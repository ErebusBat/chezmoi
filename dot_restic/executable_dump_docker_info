#!/bin/zsh
set -e
unsetopt nomatch
OUTPUT_FOLDER=$1
if [[ -z $OUTPUT_FOLDER ]]; then
  echo "USAGE: $0 [OUTPUT_FOLDER]"
  exit 1
fi
if [[ ! -d $OUTPUT_FOLDER ]]; then
  echo "FATAL: OUTPUT_FOLDER ($OUTPUT_FOLDER) does not exist"
  exit 1
fi

echo "docker ps"
docker ps --format json > $OUTPUT_FOLDER/docker_ps.json

TMP_FILE=/tmp/docker_container.json
CONTAINER_IDS=($(docker ps -q))
for cid in $CONTAINER_IDS; do
  docker container inspect $cid > $TMP_FILE
  name=$(jq '.[] .Name' $TMP_FILE)
  name=${name:gs/\//}
  name=${name:gs/\"/}
  container_file_name="container_${name}-${cid}.json"
  echo "${name} - $cid"

  # Extract Image ID, setup image inspect output file
  IMAGE_ID=$(jq -r '.[] .Image' $TMP_FILE)
  image_file_name="image_${name}-${IMAGE_ID}.json"

  # Capture image information, including the all important repo hashes
  docker image inspect $IMAGE_ID > $TMP_FILE.image

  # Cleanup older images
  file_pattern=$OUTPUT_FOLDER/container_$name-*.json
  if [[ -n $(ls ${~file_pattern} 2>/dev/null) ]]; then
    rm -f ${~file_pattern} #2>/dev/null
  fi

  # Copy into final output
  cp $TMP_FILE $OUTPUT_FOLDER/$container_file_name
  cp $TMP_FILE.image $OUTPUT_FOLDER/$image_file_name
done
